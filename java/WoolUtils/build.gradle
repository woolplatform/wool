plugins {
	id 'java'
	id 'java-library'
	id 'distribution'
	id 'eclipse-wtp'
	id 'maven-publish'
	id 'signing'
}

group = 'eu.woolplatform'
version = '2.2.2'
sourceCompatibility = 1.8
targetCompatibility = 1.8
archivesBaseName = 'wool-utils'

repositories {
	jcenter()
}

dependencies {
	api 'com.fasterxml.jackson.core:jackson-core:2.9.8'
	api 'com.fasterxml.jackson.core:jackson-annotations:2.9.8'
	api 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
	api 'joda-time:joda-time:2.10.1'
	api 'org.slf4j:slf4j-api:1.7.26'
	api 'commons-codec:commons-codec:1.3' // newer versions not supported in Android
    api fileTree(dir: 'libs', include: '*.jar')
	testImplementation 'junit:junit:4.12'
}

distributions {
	main {
		contents {
			from configurations.compileClasspath
		}
	}
}

java {
	withJavadocJar()
	withSourcesJar()
}

distributions {
	main {
		distributionBaseName = archivesBaseName
		contents {
			from configurations.compileClasspath
			from jar
		}
	}
}

[distZip, distTar, installDist]*.dependsOn jar

javadoc {
	options {
		links 'https://docs.oracle.com/javase/10/docs/api/'
		addStringOption('Xdoclint:all,-html', '-quiet')
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = archivesBaseName
			from components.java
			
			pom {
				name = 'WOOL Dialogue Framework Utils'
				description = 'General purpose utils used in the WOOL Dialogue Framework.'
				url = 'https://www.woolplatform.eu/'
				
				licenses {
					license {
						name = 'MIT License'
						url = 'https://opensource.org/licenses/MIT'
					}
				}
				
				developers {
					developer {
						id = 'bvanschooten'
						name = 'Boris van Schooten'
						email = 'b.vanschooten@rrd.nl'
					}
					developer {
						id = 'dhofs'
						name = 'Dennis Hofs'
						email = 'd.hofs@rrd.nl'
					}
					developer {
						id = 'hopdenakker'
						name = 'Harm op den Akker'
						email = 'h.opdenakker@rrd.nl'
					}
					developer {
						id = 'tbeinema'
						name = 'Tessa Beinema'
						email = 't.beinema@rrd.nl'
					}					
				}
				
				scm {
					connection = 'scm:git:git://github.com/woolplatform/wool.git'
					developerConnection = 'scm:git:ssh://github.com:woolplatform/wool.git'
					url = 'https://github.com/woolplatform/wool/tree/master'
				}
			}
		}
	}
	repositories {
		maven {
			def ossrhUsername = project.properties.ossrhUsername
			if (!ossrhUsername)
				ossrhUsername = ''
			def ossrhPassword = project.properties.ossrhPassword
			if (!ossrhPassword)
				ossrhPassword = ''
			def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
			def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
			url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
			credentials {
				username ossrhUsername
				password ossrhPassword
			}
		}
	}
}

signing {
	sign publishing.publications.mavenJava
}
