plugins {
	id 'java'
	id 'java-library'
	id 'distribution'
	id 'eclipse-wtp'
	id 'maven-publish'
	id 'signing'
}

group = 'eu.woolplatform'
version = '2.4.2'
sourceCompatibility = 11
targetCompatibility = 11
archivesBaseName = 'wool-utils'

repositories {
	mavenCentral()
}

dependencies {
	api 'com.fasterxml.jackson.core:jackson-core:2.13.4'
	api 'com.fasterxml.jackson.core:jackson-annotations:2.13.4'
	api 'com.fasterxml.jackson.core:jackson-databind:2.13.4.2'
	// newer versions of slf4j-api are not compatible with older versions of
	// logback, newer version of logback (1.2.11) are not compatible with latest
	// Spring Boot (2.7.3)
	api 'org.slf4j:slf4j-api:1.7.36'
	testImplementation 'junit:junit:4.13.2'
}

distributions {
	main {
		contents {
			from configurations.compileClasspath
		}
	}
}

java {
	withJavadocJar()
	withSourcesJar()
}

distributions {
	main {
		distributionBaseName = archivesBaseName
		contents {
			from configurations.compileClasspath
			from jar
		}
	}
}

[distZip, distTar, installDist]*.dependsOn jar

javadoc {
	options {
		links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
		addStringOption('Xdoclint:all,-html', '-quiet')
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = archivesBaseName
			from components.java
			
			pom {
				name = 'WOOL Dialogue Framework Utils'
				description = 'General purpose utils used in the WOOL Dialogue Framework.'
				url = 'https://www.woolplatform.eu/'
				
				licenses {
					license {
						name = 'MIT License'
						url = 'https://opensource.org/licenses/MIT'
					}
				}
				
				developers {
					developer {
						id = 'bvanschooten'
						name = 'Boris van Schooten'
						email = 'b.vanschooten@rrd.nl'
					}
					developer {
						id = 'dhofs'
						name = 'Dennis Hofs'
						email = 'd.hofs@rrd.nl'
					}
					developer {
						id = 'hopdenakker'
						name = 'Harm op den Akker'
						email = 'hopdenakker@innovationsprint.eu'
					}
					developer {
						id = 'tbeinema'
						name = 'Tessa Beinema'
						email = 't.c.beinema@utwente.nl'
					}					
				}
				
				scm {
					connection = 'scm:git:git://github.com/woolplatform/wool.git'
					developerConnection = 'scm:git:ssh://github.com:woolplatform/wool.git'
					url = 'https://github.com/woolplatform/wool/tree/master'
				}
			}
		}
	}
	repositories {
		maven {
			def sonatypeUsername = project.properties.ossrhUsername
			if (!sonatypeUsername)
				sonatypeUsername = ''
			def sonatypePassword = project.properties.ossrhPassword
			if (!sonatypePassword)
				sonatypePassword = ''
			def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
			def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
			url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
			credentials {
				username sonatypeUsername
				password sonatypePassword
			}
		}
	}
}

signing {
	sign publishing.publications.mavenJava
}
