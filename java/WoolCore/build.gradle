plugins {
	id 'java'
	id 'java-library'
	id 'distribution'
	id 'eclipse-wtp'
	id 'maven-publish'
	id 'signing'
}

group = 'eu.woolplatform'
version = '2.3.0'
sourceCompatibility = 11
targetCompatibility = 11
archivesBaseName = 'wool-core'

repositories {
	mavenCentral()
}

dependencies {
	implementation "eu.woolplatform:wool-utils:2.2.6" // Use the maven central distribution of WOOL Utils.
	// implementation project(':WoolUtils')           // Use the local WOOL Utils project source.
	implementation 'org.slf4j:slf4j-api:1.7.36'
	implementation 'com.fasterxml.jackson.core:jackson-core:2.13.3'
	implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.3'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.3'
	implementation 'joda-time:joda-time:2.10.14'
	testImplementation 'junit:junit:4.13.2'
}

jar {
	manifest {
		attributes 'Main-Class': 'eu.woolplatform.wool.parser.WoolParser'
		attributes 'Class-Path': configurations.compileClasspath.collect({ it.name }).join(' ')
	}
}

java {
	withJavadocJar()
	withSourcesJar()
}

distributions {
	main {
		distributionBaseName = archivesBaseName
		contents {
			from configurations.compileClasspath
			from jar
		}
	}
}

[distZip, distTar, installDist]*.dependsOn jar

javadoc {
	options {
		links 'https://docs.oracle.com/javase/10/docs/api/'
		addStringOption('Xdoclint:none', '-quiet')
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = archivesBaseName
			from components.java
			
			pom {
				name = 'WOOL Dialogue Platform'
				description = 'WOOL is a simple, powerful dialogue platform for creating virtual agent conversations.'
				url = 'https://www.woolplatform.eu/'
				
				licenses {
					license {
						name = 'MIT License'
						url = 'https://opensource.org/licenses/MIT'
					}
				}
				
				developers {
					developer {
						id = 'bvanschooten'
						name = 'Boris van Schooten'
						email = 'b.vanschooten@rrd.nl'
					}
					developer {
						id = 'dhofs'
						name = 'Dennis Hofs'
						email = 'd.hofs@rrd.nl'
					}
					developer {
						id = 'hopdenakker'
						name = 'Harm op den Akker'
						email = 'hopdenakker@innovationsprint.eu'
					}
					developer {
						id = 'tbeinema'
						name = 'Tessa Beinema'
						email = 't.c.beinema@utwente.nl'
					}					
				}
				
				scm {
					connection = 'scm:git:git://github.com/woolplatform/wool.git'
					developerConnection = 'scm:git:ssh://github.com:woolplatform/wool.git'
					url = 'https://github.com/woolplatform/wool/tree/master'
				}
			}
		}
	}
	repositories {
		maven {
			def sonatypeUsername = project.properties.ossrhUsername
			if (!sonatypeUsername)
				sonatypeUsername = ''
			def sonatypePassword = project.properties.ossrhPassword
			if (!sonatypePassword)
				sonatypePassword = ''
			def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
			def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
			url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
			credentials {
				username sonatypeUsername
				password sonatypePassword
			}
		}
	}
}

signing {
	sign publishing.publications.mavenJava
}
